; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Reagent Strip Test"
#define MyAppVersion    "1.0.0925.2"
#define MyAppPublisher ""
#define MyAppURL ""
#define MyAppExeName "ReagentStripTest.exe"
#define dotNetFramework "NDP461-KB3102436-x86-x64-AllOS-ENU.exe"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B7E820CA-6C8A-402F-BFB1-B596475F72D5}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={localappdata}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputDir=上位机\{#MyAppName}\{#MyAppVersion}
OutputBaseFilename={#MyAppName}-{#MyAppVersion}-setup
Compression=lzma
SolidCompression=yes
UsePreviousAppDir=no

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "chs"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"
Name: "cht"; MessagesFile: "compiler:Languages\ChineseTraditional.isl"
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"
Name: "de"; MessagesFile: "compiler:Languages\German.isl"
Name: "pt"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "ru"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "es"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "it"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "ja"; MessagesFile: "compiler:Languages\Japanese.isl"

[CustomMessages]
en.ConfirmInstallDependence=The software depends %1，would you like to install it?
chs.ConfirmInstallDependence=该软件依赖于%1，是否安装%1？
cht.ConfirmInstallDependence=w依於%1，是否安b%1？

en.ConfirmDeleteConfig=Do you want to keep configuration files? 
chs.ConfirmDeleteConfig=你是否想保留配置文件？
cht.ConfirmDeleteConfig=你是否想保留O定n？


[Code]

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep); 
var configFilePath:String;
begin
  configFilePath := ExpandConstant('{app}') + '\SystemDefaultConfig.xml'
  if FileExists(configFilePath) and (CurUninstallStep = usUninstall) then
  begin
    if MsgBox(CustomMessage('ConfirmDeleteConfig'), mbConfirmation, MB_YESNO) = idNo then
    begin
      DeleteFile(configFilePath)
    end;
  end;
end;

function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1'          .NET Framework 1.1
//    'v2.0'          .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//    'v4.5.1'        .NET Framework 4.5.1
//    'v4.5.2'        .NET Framework 4.5.2
//    'v4.6'          .NET Framework 4.6
//    'v4.6.1'        .NET Framework 4.6.1
//    'v4.6.2'        .NET Framework 4.6.2
//    'v4.7'          .NET Framework 4.7
//    'v4.7.1'        .NET Framework 4.7.1
//    'v4.7.2'        .NET Framework 4.7.2
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key, versionKey: string;
    install, release, serviceCount, versionRelease: cardinal;
    success: boolean;
begin
    versionKey := version;
    versionRelease := 0;

    // .NET 1.1 and 2.0 embed release number in version key
    if version = 'v1.1' then begin
        versionKey := 'v1.1.4322';
    end else if version = 'v2.0' then begin
        versionKey := 'v2.0.50727';
    end

    // .NET 4.5 and newer install as update to .NET 4.0 Full
    else if Pos('v4.', version) = 1 then begin
        versionKey := 'v4\Full';
        case version of
          'v4.5':   versionRelease := 378389;
          'v4.5.1': versionRelease := 378675; // 378758 on Windows 8 and older
          'v4.5.2': versionRelease := 379893;
          'v4.6':   versionRelease := 393295; // 393297 on Windows 8.1 and older
          'v4.6.1': versionRelease := 394254; // 394271 before Win10 November Update
          'v4.6.2': versionRelease := 394802; // 394806 before Win10 Anniversary Update
          'v4.7':   versionRelease := 460798; // 460805 before Win10 Creators Update
          'v4.7.1': versionRelease := 461308; // 461310 before Win10 Fall Creators Update
          'v4.7.2': versionRelease := 461808; // 461814 before Win10 April 2018 Update
          'v4.8'  : versionRelease := 528040; // 461814 before Win10 April 2018 Update
        end;
    end;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + versionKey;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0 and newer use value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 and newer use additional value Release
    if versionRelease > 0 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= versionRelease);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;


 

 // 检查Virtual COM Port Driver是否已安装
 function IsVirtualComPortDriverInstalled: boolean;
 begin
  result := RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Classes\Installer\Products\F99235898DA7065489690656B08DCAFB')
 end;


// function IsSciU2SInstalled: boolean;
// begin
//   result := RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\B6AAE7158C952C97E77091B70DAAAC12A9554D3E')
// end;

// 如果没有安装.Net4.5安装
function InitializeSetup(): Boolean;
var Path:String;
  ResultCode: Integer;
begin
  if not IsDotNetDetected('v4.6.1', 0) then
  begin
    if MsgBox(FmtMessage(CustomMessage('ConfirmInstallDependence'), ['.Net Framework v4.6.1']) , mbConfirmation, MB_YESNO) = idYes then
    begin
      ExtractTemporaryFile('{#dotNetFramework}');
      Exec(ExpandConstant('{tmp}\{#dotNetFramework}'), '', '', SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);
      if IsDotNetDetected('v4.6.1', 0) then
        Result := true
      else
        Result := false
    end
    else
      Result := false;
  end
  else
    Result := true;
end;


[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: 

[Files]
Source: "ReagentStripTest\bin\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "ReagentStripTest\bin\Release\ReagentStripTest.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "ReagentStripTest\bin\Release\ReagentStripTest.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "ReagentStripTest\bin\Release\logo.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "ReagentStripTest\bin\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "MasTools\bin\Release\x86\*"; DestDir: "{app}\x86"; Flags: ignoreversion
;Source: "MasTools\bin\Release\x64\*"; DestDir: "{app}\x64"; Flags: ignoreversion
;Source: "Public\SciU2S_Driver\*"; DestDir: "{app}\drivers\SciU2S_Driver"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Public\VCP_V1.5.0_32bits.exe"; DestDir: "{app}\drivers"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Public\VCP_V1.5.0_64bits.exe"; DestDir: "{app}\drivers"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Public\CH341SER.EXE"; DestDir: "{app}\drivers"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Public\{#dotNetFramework}"; DestDir: "{tmp}"; Flags: ignoreversion


; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\logo.ico"
;Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; IconFilename: "{app}\logo.ico"

[Run]
;Filename: "{app}\drivers\SciU2S_Driver\x86\DPInst.exe"; Description: "{cm:LaunchProgram,Install driver}"; Flags: skipifsilent hidewizard; Check:not IsWin64
;Filename: "{app}\drivers\SciU2S_Driver\x64\DPInst.exe"; Description: "{cm:LaunchProgram,Install driver}"; Flags: skipifsilent hidewizard; Check:IsWin64
Filename: "{app}\drivers\VCP_V1.5.0_32bits.exe"; Description: "{cm:LaunchProgram,Install driver}"; Flags: skipifsilent hidewizard; Check:not IsWin64 and not IsVirtualComPortDriverInstalled
Filename: "{app}\drivers\VCP_V1.5.0_64bits.exe"; Description: "{cm:LaunchProgram,Install driver}"; Flags: skipifsilent hidewizard; Check:IsWin64 and not IsVirtualComPortDriverInstalled

;Filename: "{app}\drivers\CH341SER.EXE"; Description: "{cm:LaunchProgram,Install driver}"; Flags: skipifsilent hidewizard;
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent 

[UninstallDelete]
Type: files; Name: "{app}\{#MyAppExeName}.config"
Type: files; Name: "{app}\*.log"
Type: files; Name: "{app}\*.db"
Type: files; Name: "{app}\*.json"
Type: filesandordirs; Name: "{app}\backup"
Type: dirifempty; Name: "{app}\Library"
Type: dirifempty; Name: "{app}"